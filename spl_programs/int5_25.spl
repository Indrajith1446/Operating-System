breakpoint;
alias userSP R1;
userSP=SP;
alias pid R8;
pid=[SYSTEM_STATUS_TABLE+1];
[PROCESS_TABLE+pid*16+13]=SP;
SP=[PROCESS_TABLE+pid*16+11]*512-1;
alias syscallno R2;
syscallno=[([PTBR+2*((userSP-5)/512)]*512)+((userSP-5)%512)];
alias ra R7;
ra=([PTBR+2*((userSP-1)/512)]*512)+((userSP-1)%512);
//open
if(syscallno==2) then
	[PROCESS_TABLE+pid*16+9]=2;
	alias filename R3;
	filename=[([PTBR+2*((userSP-4)/512)]*512)+((userSP-4)%512)];
	alias i R4;
	alias uap R5;
	uap=[PROCESS_TABLE+pid*16+11];
	i=0;
	while(i<8) do
		if([uap*512+496+2*i]==-1) then
			break;
		endif;
		i=i+1;
	endwhile;
	if(i==8) then
		[ra]=-3;
		goto exit;
	endif;
	multipush(R1,R2,R3,R4,R5,R7,R8);
	R1=3;
	R2=filename;
	call MOD_3;
	multipop(R1,R2,R3,R4,R5,R7,R8);
	alias filei R6;
	filei=R0;
	if(filei<0) then
		[ra]=filei;
		goto exit;
	endif;
	[uap*512+496+2*i]=0;
	[uap*512+496+2*i+1]=filei;
	[ra]=i;
	goto exit;
endif;
//close
if(syscallno==3) then
	[PROCESS_TABLE+pid*16+9]=3;
	alias fd R3;
	fd=[([PTBR+2*((userSP-4)/512)]*512)+((userSP-4)%512)];
	if(fd<0||fd>7) then
		[ra]=-1;
		goto exit;
	endif;
	alias uap R4;
	uap=[PROCESS_TABLE+pid*16+11];
	if([uap*512+496+2*fd+1]==-1||[uap*512+496+2*fd]!=0) then
		[ra]=-1;
		goto exit;
	endif;
	alias i R5;
	i=[uap*512+496+2*fd+1];
	multipush(R1,R2,R3,R4,R5,R7,R8);
	R1=4;
	R2=i;
	call MOD_3;
	multipop(R1,R2,R3,R4,R5,R7,R8);
	[uap*512+496+2*fd]=-1;
	[uap*512+496+2*fd+1]=-1;
	[ra]=0;
	goto exit;
endif;
//seek system call
if(syscallno==6) then
	[PROCESS_TABLE+pid*16+9]=6;
	alias fd R3;
	fd=[([PTBR+2*((userSP-4)/512)]*512)+((userSP-4)%512)];
	if(fd<0||fd>7) then
		[ra]=-1;
		goto exit;
	endif;
	alias uap R4;
	uap=[PROCESS_TABLE+pid*16+11];
	alias i R5;
	i=[uap*512+496+2*fd+1];
	alias inodei R6;
	inodei=[OPEN_FILE_TABLE+4*i];
	multipush(R1,R2,R3,R4,R5,R6,R7,R8);
	R1=4;
	R2=inodei;
	R3=pid;
	call MOD_0;
	multipop(R1,R2,R3,R4,R5,R6,R7,R8);
	if(R0==-1) then
		[ra]=-1;
		goto exit;
	endif;
	alias lseek R9;
	lseek=[OPEN_FILE_TABLE+4*i+2];
	alias fsize R10;
	if([OPEN_FILE_TABLE+4*i]==INODE_ROOT) then
		fsize=480;
	else
		fsize=[INODE_TABLE+inodei*16+2];
	endif;
	alias offset R11;
	offset=[([PTBR+2*((userSP-3)/512)]*512)+((userSP-3)%512)];
	if(lseek+offset<0) then
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
		R1=5;
		R2=inodei;
		R3=pid;
		call MOD_0;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
		[ra]=-2;
		goto exit;
	endif;
	if(offset==0) then
		[OPEN_FILE_TABLE+4*i+2]=0;
	else
		if(lseek+offset>fsize) then
			[OPEN_FILE_TABLE+4*i+2]=fsize;
		else
			[OPEN_FILE_TABLE+4*i+2]=lseek+offset;
		endif;
	endif;
	multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
	R1=5;
	R2=inodei;
	R3=pid;
	call MOD_0;
	multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11);
	[ra]=0;
	goto exit;
endif;
exit:
	SP=userSP;
	[PROCESS_TABLE+pid*16+9]=0;
	ireturn;
